// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ IMPORTS ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;



// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ CODE ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

parser code {:

	Logger log = Logger.getLogger(getClass());
   
    // Redefined error handling methods
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(String message, Symbol cur_token) {
        report_error("\nSyntax error: " + message, cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}



// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ GRAMMAR ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~




// ~~~~~~~~~~~~~~~~NONTERMINALS~~~~~~~~~~~~~~~~

nonterminal Program, ProgramDeclListE, MethodDeclListE;
nonterminal ConstDecl, VarDecl, ClassDecl;
nonterminal ConstDeclInnerList, Type, Const;
nonterminal VarDeclInnerList, SquareBracketsOptional;
nonterminal ExtendsTypeOptional, ClassBody;
nonterminal VarDeclListE, ClassConstructorsAndMethodsDeclOptional;
nonterminal ConstructorDeclListE, ConstructorDecl, MethodDecl;
nonterminal MethodReturnType, FormParsOptional, StatementListE;
nonterminal FormPars;
nonterminal Statement, MatchedStatement, UnmatchedStatement;
nonterminal Condition;
nonterminal DesignatorStatement, ExprOptional, PrintNumConstOptional;
nonterminal Expr;
nonterminal Designator, DesignatorAction, DesignatorListOptionalES;
nonterminal ActParsOptional, Assignop;
nonterminal DesignatorList;
nonterminal ActPars;
nonterminal CondTerm, CondFact;
nonterminal RelopExprOptional, Relop;
nonterminal MinusOptional, TermAddopList, Addop, Term;
nonterminal Mulop, Factor;
nonterminal ParenActParsOptional, NewTypeSuffix;
nonterminal Label;
nonterminal VarDeclGlobal, VarDeclInnerListGlobal;
nonterminal DesignatorAssignopExpr;

nonterminal ProgramName;

nonterminal ConstDeclInnerListElement, VarDeclInnerListElement, FormParam, DesignatorOptional, ForeachStatement, ConditionInIf;




// ~~~~~~~~~~~~~~~~TERMINALS~~~~~~~~~~~~~~~~

// Keywords
terminal PROGRAM, BREAK, CLASS, ELSE, CONST, IF, WHILE, NEW, PRINT, READ, RETURN, VOID, EXTENDS, CONTINUE, FOREACH;

// Operators
terminal PLUS, MINUS, MUL, DIV, MOD, EQUALS, NOT_EQUALS, GREATER, GREATER_EQUALS, LESS, LESS_EQUALS, AND, OR, ASSIGN;
terminal INC, DEC, SEMI_COLON, COLON, COMMA, DOT, L_PAREN, R_PAREN, L_SQUARE_BRACKET, R_SQUARE_BRACKET, L_BRACE, R_BRACE, ARROW;

// Token types
terminal CHAR_CONST, BOOL_CONST, NUM_CONST, IDENT;



// ~~~~~~~~~~~~~~~~PRECEDENCE~~~~~~~~~~~~~~~~

precedence left ELSE;




// ~~~~~~~~~~~~~~~~RULES~~~~~~~~~~~~~~~~

Program ::= (Program) PROGRAM ProgramName ProgramDeclListE L_BRACE MethodDeclListE R_BRACE;

ProgramName ::= (ProgramName) IDENT:programName;

ProgramDeclListE ::= (ProgramDeclListEConst) ProgramDeclListE ConstDecl
				|
				(ProgramDeclListEVar) ProgramDeclListE VarDeclGlobal
				|
				(ProgramDeclListEClass) ProgramDeclListE ClassDecl
				|
				(ProgramDeclListEEmpty) /* e */
				;
				
ConstDecl ::= (ConstDecl) CONST Type ConstDeclInnerList SEMI_COLON;

ConstDeclInnerList ::= (ConstDeclInnerListSingle) ConstDeclInnerListElement
			|
			(ConstDeclInnerListMultiple) ConstDeclInnerList COMMA ConstDeclInnerListElement
			;

ConstDeclInnerListElement ::= (ConstDeclInnerListElement) IDENT ASSIGN Const;

Const ::= (NumConst) NUM_CONST
		|
		(BoolConst) BOOL_CONST
		|
		(CharConst) CHAR_CONST
		;
		
VarDeclGlobal ::= (VarDeclGlobalExists) VarDecl
				|
				(VarDeclGlobalError) error:e 
					{: 
						parser.syntax_error("Globar var declaration error at line: " + eleft, null); 
						parser.log.info("Global var declaration error recovery successful."); 
					:}
				;

VarDecl ::= (VarDecl) Type VarDeclInnerList SEMI_COLON;

//VarDeclInnerListGlobal ::= (VarDeclInnerListGlobalSingle) IDENT SquareBracketsOptional
//				|
//				(VarDeclInnerListGlobalMultiple) VarDeclInnerListGlobal COMMA IDENT SquareBracketsOptional
//				|
//				(VarDeclInnerListGlobalError) error:e {: parser.report_error("Globar var declaration error at line: " + eleft, null); :}
//				;

VarDeclInnerList ::= (VarDeclInnerListSingle) VarDeclInnerListElement
				|
				(VarDeclInnerListMultiple) VarDeclInnerList COMMA VarDeclInnerListElement
				;
				
VarDeclInnerListElement ::= IDENT SquareBracketsOptional;

SquareBracketsOptional ::= (SquareBracketsExists) L_SQUARE_BRACKET R_SQUARE_BRACKET
						|
						(SquareBracketsEmpty) /* e */ 
						;
						
						
						
ClassDecl ::= (ClassDecl) CLASS IDENT ExtendsTypeOptional L_BRACE ClassBody R_BRACE;

ExtendsTypeOptional ::= (ExtendsTypeExists) EXTENDS Type
					|
					(ExtendsTypeEmpty) /* e */
					|
					(ExtendsTypeError) error:e 
						{: 
							parser.syntax_error("Class extends type error at line: " + eleft, null); 
							parser.log.info("Class extends type error recovery successful."); 
						:}
					;

ClassBody ::= (ClassBody) VarDeclListE ClassConstructorsAndMethodsDeclOptional;

VarDeclListE ::= (VarDeclListEExists) VarDeclListE VarDecl
			|
			(VarDeclListEEmpty) /* e */
			;

ClassConstructorsAndMethodsDeclOptional ::= (ClassConstructorsAndMethodsDeclOptionalExists) L_BRACE /* ConstructorDeclListE */ MethodDeclListE R_BRACE
										|
										(ClassConstructorsAndMethodsDeclOptionalEmpty) /* e */
										;

// Level C
//ConstructorDeclListE ::= (ConstructorDeclListEExists) ConstructorDeclListE ConstructorDecl
//						|
//						(ConstructorDeclListEEmpty) /* e */
//						;
//
//ConstructorDecl ::= (ConstructorDeclEmpty) /* e */; // Level C


MethodDeclListE ::= (MethodDeclListEExists) MethodDeclListE MethodDecl
				|
				(MethodDeclListEEmpty) /* e */
				;

MethodDecl ::= (MethodDecl) MethodReturnType IDENT L_PAREN FormParsOptional R_PAREN VarDeclListE L_BRACE StatementListE R_BRACE;

MethodReturnType ::= (MethodReturnTypeHasType) Type
					|
					(MethodReturnTypeIsVoid) VOID
					;
					
FormParsOptional ::= (FormParsExists) FormPars
					|
					(FormParsEmpty) /* e */
					;
					
FormPars ::= (FormParsSignle) FormParam
			|
			(FormParsMultiple) FormPars COMMA FormParam
			|	
			(FormParsError) error:e 
				{: 
					parser.syntax_error("Formal parameter declaration error at line: " + eleft, null); 
					parser.log.info("Formal parameter declaration error recovery successful."); 
				:}
			;
			
FormParam ::= Type IDENT SquareBracketsOptional;

Type ::= (Type) IDENT;

StatementListE ::= (StatementListEExists) StatementListE Statement
				|
				(StatementListEEmpty) /* e */
				;

Statement ::= (StatementMatched) MatchedStatement
			|
			(StatementUnmatched) UnmatchedStatement
			;
			
UnmatchedStatement ::= (UnmatchedStatementIf) IF L_PAREN ConditionInIf R_PAREN Statement
					|
					(UnmatchedStatementIfElse) IF L_PAREN ConditionInIf R_PAREN MatchedStatement ELSE UnmatchedStatement
					;

MatchedStatement ::= (MatchedStatementDesignator) DesignatorStatement SEMI_COLON
					|
					(MatchedStatementIfElse) IF L_PAREN ConditionInIf R_PAREN MatchedStatement ELSE MatchedStatement 
					|
					(MatchedStatementWhile) WHILE L_PAREN Condition R_PAREN Statement
					|
					(MatchedStatementBreak) BREAK SEMI_COLON
					|
					(MatchedStatementContinue) CONTINUE SEMI_COLON
					|
					(MatchedStatementExpr) RETURN ExprOptional SEMI_COLON
					|
					(MatchedStatementRead) READ L_PAREN Designator R_PAREN SEMI_COLON
					|
					(MatchedStatementPrint) PRINT L_PAREN Expr PrintNumConstOptional R_PAREN SEMI_COLON
					|
					(MatchedStatementForeach) ForeachStatement SEMI_COLON
					|
					(MatchedStatementSection) L_BRACE StatementListE R_BRACE
					;
					
ForeachStatement ::= (ForeachStatement) Designator DOT FOREACH L_PAREN IDENT ARROW Statement R_PAREN;

ExprOptional ::= (ExprOptionalExists) Expr
				|
				(ExprOptionalEmpty) /* e */
				;


PrintNumConstOptional ::= (PrintNumConstOptionalExists) COMMA NUM_CONST
						|
						(PrintNumConstOptionalEmpty) /* e */
						;
						
DesignatorStatement ::= (DesignatorStatementAction) Designator DesignatorAction 
					|
					(DesignatorStatementSquareBracket) L_SQUARE_BRACKET DesignatorListOptionalES R_SQUARE_BRACKET ASSIGN Designator
					|	
					(DesignatorStatementError) error:e 
						{: 
							parser.syntax_error("Designator statement error at line: " + eleft, null); 
							parser.log.info("Designator statement error recovery successful."); 
						:}
					;
					
					
DesignatorAction ::= (DesignatorActionAssignop) DesignatorAssignopExpr
					|
					(DesignatorActionActPars) L_PAREN ActParsOptional R_PAREN
					|
					(DesignatorActionInc) INC
					|
					(DesignatorActionDec) DEC
					;
					
DesignatorAssignopExpr ::= (DesignatorAssignopExpression) Assignop Expr
						|
						(DesignatorAssignopExprError) error:e {: parser.report_error("Assignment operation error at line: " + eleft, null); :}
						;
					
					
DesignatorListOptionalES ::= (DesignatorListOptionalESExist) DesignatorOptional
						|
						(DesignatorListOptionalESEmpty) DesignatorListOptionalES COMMA DesignatorOptional
						;
										
DesignatorOptional ::= (DesignatorOptionalExist) Designator
					|
					(DesignatorOptionalEmpty) /* e */
					;
				
ActPars ::= (ActParsMultiple) ActPars COMMA Expr
		|
		(ActParsSingle) Expr
		;
		
ActParsOptional ::= (ActParsOptionalExist) ActPars
				|
				(ActParsOptionalEmpty) /* e */
				;

ConditionInIf ::= (ConditionInIfSucess) Condition
				|
				(ConditionInIfError) error:e 
					{: 
						parser.syntax_error("Statement in If Condition error at line: " + eleft, null); 
						parser.log.info("Statement in If Condition error recovery successful."); 
					:}
				;

Condition ::= (ConditionMultiple) Condition OR CondTerm
			|
			(ConditionSingle) CondTerm
			;
			
CondTerm ::= (CondTermMultiple) CondTerm AND CondFact
			|
			(CondTermSingle) CondFact
			;

CondFact ::= (CondFact) Expr RelopExprOptional;

RelopExprOptional ::= (RelopExprOptionalExist) Relop Expr
					|
					(RelopExprOptionalEmpty) /* e */
					;

Expr ::= (ExprSingle) MinusOptional Term
		|
		(ExprMultiple) Expr Addop Term
		;

MinusOptional ::= (MinusOptionalExist) MINUS
				|
				(MinusOptionalEmpty) /* e */
				;
				
				
Term ::= (TermMultiple) Term Mulop Factor
		|
		(TermSingle) Factor
		;
			
Factor ::= (FactorDesignator) Designator ParenActParsOptional
		| 
		(FactorNumConst) NUM_CONST
		| 
		(FactorCharConst) CHAR_CONST
		| 
		(FactorBoolConst) BOOL_CONST
		|
		(FactorNew) NEW Type NewTypeSuffix
		|
		(FactorExpr) L_PAREN Expr R_PAREN
		;
		
ParenActParsOptional ::= (ParenActParsOptionalExist) L_PAREN ActParsOptional R_PAREN
						|
						(ParenActParsOptionalEmpty) /* e */
						;

NewTypeSuffix ::= (NewTypeSuffixSquareBracket) L_SQUARE_BRACKET Expr R_SQUARE_BRACKET
				|
				(NewTypeSuffixActPars) L_PAREN ActParsOptional R_PAREN
				;		
				
Designator ::= (DesignatorMultipleIdent) Designator DOT IDENT
			|
			(DesignatorMultipleExpr) Designator L_SQUARE_BRACKET Expr R_SQUARE_BRACKET
			|
			(DesignatorSingle) IDENT
			;
			
Label ::= (Label) IDENT;

Assignop ::= (Assignop) ASSIGN;

Relop ::= (RelopEquals) EQUALS
		|
		(RelopNotEquals) NOT_EQUALS
		|
		(RelopGreater) GREATER
		|
		(RelopGreaterEquals) GREATER_EQUALS
		|
		(RelopLess) LESS
		|
		(RelopLessEquals) LESS_EQUALS
		;
	
Addop ::= (AddopPlus) PLUS
		|
		(AddopMinus) MINUS
		;
		
Mulop ::= (AddopMul) MUL
		|
		(AddopDiv) DIV
		|
		(AddopMod) MOD
		;	
		
		
		